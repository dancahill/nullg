include_template("common.ns");
include_template("class.html_table.ns");
include_template("db.ns");

function search_contacts() {
	var maxlist   = tonumber(_USER['pref']['maxlist']);
	var menustyle = tonumber(_USER['pref']['menustyle']);
	var userid    = tonumber(_USER['userid']);

	global Q=db.contacts.getlist();
	print("<br>\r\n");
	if (Q._tuples<1) {
		print("<b>Found 0 matching contacts</b>\r\n");
		return;
	}
	t=new html_table();
	t.datasource=Q;
	t.tablename="T2";
	t.tableclass="contenttable";
	t.autoaddcolumns=false;
	fntd=function (row, column) {
		return sprintf("<td onclick=\"javascript:contacts.view(%d)\"'>", tonumber(row['contactid']));
	};
	fn=function (row, column) { id=tonumber(row['contactid']); sn=row['surname']; gn=row['givenname']; name=sn+((sn!=""&&gn!="")?", ":"")+gn; if (name=="") return "";
		return sprintf("<a href=\"javascript:contacts.view(%d);\">%s</a>", id, str2html(name));
	};
	t.addcolumn("surname", "Contact Name", fn, fntd);
	t.addcolumn("organization", "Company Name");
	t.addcolumn("worknumber", "Work Number");
	if (menustyle>0) {
		fn=function (row, column) {
			email=row['email'];
			if (email=="") return "";
			sn=row['surname']; gn=row['givenname']; name=gn+((sn!=""&&gn!="")?" ":"")+sn;
			return "<a href=\"javascript:MsgTo('&quot;"+str2html(name)+"&quot; <"+email+">')\">"+str2html(email)+"</a>";
		};
		t.addcolumn("email", "E-Mail", fn);
	} else {
		fntd=function (row, column) {
			email=row['email'];
			if (email=="") return "<td>";
			gn=row['givenname']; sn=row['surname']; name=gn+((gn!=""&&sn!="")?" ":"")+sn;
			return "<td onclick=\"javascript:contacts.emailaddrbook.add('&quot;"+str2html(name)+"&quot; <"+email+">');\">";
		};
		fn=function (row, column) {
			email=row['email'];
			if (email=="") return "";
			gn=row['givenname']; sn=row['surname']; name=gn+((gn!=""&&sn!="")?" ":"")+sn;
			return "<a href=\"javascript:contacts.emailaddrbook.add('&quot;"+str2html(name)+"&quot; <"+email+">');\">"+str2html(email)+"</a>";
		};
		t.addcolumn("email", "E-Mail", fn, fntd);
	}
	fn=function (row, column) { website=row['website']; if (website=="") return ""; return sprintf("<a href=\"%s\" target=\"_blank\">visit</a>", website); };
	t.addcolumn("website", "Web", fn);
	t.write();
	return;
}

try {
	print("<center>\r\n");
	search_contacts();
	print("</center>\r\n");
} catch (e) {
	print("Exception: [",e.description,"]");
}
