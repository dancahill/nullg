<?nsp

function list_zones() {
	ui=new db.userinfo();
	domainid=ui.domainid;
	if (ui.auth('domainadmin', 'admin') && _GET['DOMAINID']!=null) {
		domainid = tonumber(_GET['DOMAINID']);
	}
	Q=db.admin.zones.getlist(domainid);
	if (Q.rows.length()<1) {
		print("<b>Found 0 matching zones</b><br/>\r\n");
		return;
	}
	t=new html_table();
	t.datasource=Q;
	t.tablename="T2";
	t.tableclass="contenttable";
	t.autoaddcolumns=false;
	fntd=function (row, column) {
		url=sprintf("/app/admin/zone?id=%d", tonumber(row['zoneid']));
		return sprintf("<td onclick=\"return ns.dialog.create('%s');\">", url);
	};
	fn=function (row, column) {
		url=sprintf("/app/admin/zone?id=%d", tonumber(row['zoneid']));
		return sprintf("<a class=\"dialog\" href=\"%s\">%s</a>", url, strtohtml(row['zonename']));
	};
	t.addcolumn("zonename", "Zone Name", fn, fntd);
	t.write();
	printf("<a class=\"dialog\" href=\"/app/admin/zone?id=0&domainid=%d\">%s</a>", domainid, strtohtml("New Zone"));
	return;
}

try {
	include_template("common.ns");
	include("admin.lib.ns");
	admin.html.headermenu();
	admin.html.htselect_domainfilter();
	print("<center>\r\n");
	list_zones();
	print("</center>\r\n");
} catch (e) {
	print("Exception: [",e.description,"]");
}
?>